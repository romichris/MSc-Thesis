/* 
 * Copyright 2021 sasLang contributors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

include "org.mal-lang.icsLang/icsLang.mal"

category sclHierarchy {
	asset Bay extends PhysicalZone 
	user info: "Asset taken from SCL. Assets of substations are often segmented into bays."
	{
	}
	asset Substation extends PhysicalZone 
	user info: "Asset taken from SCL-Lang. An entire substation."
	{
	}
    	asset VoltageLevel extends PhysicalZone 
	user info: "Asset taken from SCL-Lang. A substations equipment are often divided into several voltage levels."
	{	
	}
}
category physical {
	asset AccessPoint extends ConnectionRule 
	user info: "Asset taken from SCL. AccessPoint is IEC 61850 terminology and means a physical or virtual interface."
	{
		| logicalConnection
			-> iedRouter.logicalConnection
	}
	asset ActuatorCB extends Actuator 
	user info: "Asset required to add attack steps from the actuator to a CB."
	modeller info: "The +> adds attack steps on top of the parent asset ones. -> would override instead."
	{
		| manipulate
			+> circuitBreaker.manipulationOfControl
		| block  
			+> circuitBreaker.lossOfControl
	}
	asset CircuitBreaker extends Equipment
	user info: "Asset taken from the review of industry SAS designs. Close or open a circuit."
	{
	}
	asset Clock extends IcsApplication 
	user info: "Taken from SCL-Lang."
	{
		| logicalConnection 
			-> accessPoint.attemptConnectToApplications
	}
	asset Equipment extends PhysicalObject
	user info: "Asset taken from SCL-Lang. Components that conduct electricity, circuit breaker etc."
	{
	// Start of IcsLangs Impact Attack steps
		| attemptPreemptiveShutdown @hidden
        user info: "Taken from icsLang. Intermediate attack step for modelling the possibility of detecting a disruption(loss of Control/View) and preemptively shutting down the system in order to prevent damage."
        ->  attemptShutdown,
            attemptUnsafeState

      | attemptPreemptiveShutdownOnSafetyLoss @hidden
        user info: "Taken from icsLang. Intermediate attack step for modelling the possibility of detecting a disruption(loss of Safety) and preemptively shutting down the system because of concerns. The loss of safety alone does not trigger either a shutdown or generate an unsafe state as control and view are expected to maintain the system operating within normal parameters."
        ->  attemptShutdown

      | attemptShutdown @hidden [Bernoulli(0.1)]
        developer info: "Taken from icsLang. This is the actual preemptive shutdown operation. The specific probability should be researched more and maybe defined by the modeller."
        ->  shutdown

      | attemptUnsafeState @hidden [Bernoulli(0.1)]
        developer info: "Taken from icsLang. This attack step models the possibility that a naturally occurring unsafe state is reached if there is a loss of control or view. The specific probability should be researched more and maybe defined by the modeller."
        ->  unsafeState

      | unsafeState @hidden
        developer info: "Taken from icsLang. The system can only be damaged if it has reached an unsafe operating state. If the safety mechanisms are still functional the system will shutdown without taking damage, otherwise the damage done to the system will bring it down."
        ->  damageToProperty,
            shutdown

      | lossOfSafety
        user info: "Taken from icsLang. Adversaries may cause loss of safety whether on purpose or as a consequence of actions taken to accomplish an operation."
        developer info: "MITRE ATT&CK ICS T880."
        ->  safetyMechanismsOffline,
            attemptPreemptiveShutdownOnSafetyLoss

      | safetyMechanismsOffline @hidden
        user info: "Taken from icsLang. Physical assets should only be damaged if the safety mechanisms fail to shutdown the system to prevent damage"
        ->  damageToProperty

      | shutdown {A}
        user info: "Taken from icsLang. Shutdown the system. Can be initiated by the attacker intentionally to disrupt the industrial process or unintentionally by tampering with system and accidentally triggering the safety shutdown procedures. If the staff detect anomalous behaviour and they can decide to preemptively shut the system down to prevent potential damage."
        ->  lossOfAvailability

      & damageToProperty {I, A}
        user info: "Taken from icsLang. Adversaries may cause damage and destruction of property to infrastructure, equipment, and the surrounding environment when attacking control systems."
        developer info: "MITRE ATT&CK ICS T879."
        ->  shutdown,
            lossOfProductivityAndRevenue

      | lossOfControl {A}
        user info: "Taken from icsLang. Adversaries may seek to achieve a sustained loss of control or a runaway condition in which operators cannot issue any commands even if the malicious interference has subsided."
        developer info: "MITRE ATT&CK ICS T827."
        ->  attemptPreemptiveShutdown

      | lossOfView {A}
        user info: "Taken from icsLang. Adversaries may cause a sustained or permanent loss of view where the ICS will require local, hands-on operator intervention."
        developer info: "MITRE ATT&CK ICS T829."
        ->  attemptPreemptiveShutdown

      | lossOfAvailability {A}
        user info: "Taken from icsLang. Adversaries may attempt to disrupt essential components or systems to prevent owner and operator from delivering products or services."
        developer info: "MITRE ATT&CK ICS T826."
        ->  lossOfProductivityAndRevenue

      | lossOfProductivityAndRevenue
        user info: "Taken from icsLang. Adversaries may cause loss of productivity and revenue through disruption and even damage to the availability and integrity of control system operations, devices, and related processes."
        developer info: "MITRE ATT&CK ICS T828."

      | manipulationOfControl {I, A}
        user info: "Taken from icsLang. Adversaries may manipulate control systems devices or possibly leverage their own, to communicate with and command physical control processes."
        developer info: "MITRE ATT&CK ICS T831."
        ->  unsafeState

      | manipulationOfView {I, A}
        user info: "Taken from icsLang. Adversaries may attempt to manipulate the information reported back to operators or controllers."
        developer info: "MITRE ATT&CK ICS T832."
        ->  unsafeState
	// END of IcsLangs Impact Attack steps
	}
	asset Function 
	user info: "Asset taken from SCL-Lang. Any additional functions of the substation. Can be unrelated to energy."
	{
		| execution
			-> logicalNode.manipulationOfControl
		
	}
	asset Gateway extends IcsApplication 
	user info: "Asset taken from the review of industry SAS designs. Communicates externally and has the function to translate protocols."	
	{
	}
	asset IEDSystem extends IcsSystem 
	user info: "Taken from SCL-Lang."
	{	
	}	
	asset IEDRouter extends IcsSystem 
	user info: "According to SCL, an IED with routing capabilities that can connect different subnetworks with accesspoints. "
	{	
		| logicalConnection
			-> accessPoint.attemptConnectToApplications
	}	
	asset LogicalDevice extends IcsApplication 
	user info: "Asset taken from SCL-Lang. Resides in a Server and contains LogicalNodes."
	{
		
	}
	asset LogicalNode extends IcsApplication 
	user info: "Taken from SCL-Lang. Resides in a LogicalDevice and enables the automation process."
	{
		| manipulationOfControl
			+> equipment.manipulationOfControl,
				actuator.manipulate
		| lossOfControl
			+> equipment.manipulationOfControl,
				actuator.block
	}

	asset MU extends IcsSystem 
	user info: "A Merging Unit (MU) merge the analog signals and translate these to electronic signals. Asset taken from the review of industry SAS designs."
	{
	}
	asset PhysicalObject  
	{
	}
	asset RTU extends IcsSystem	
	user info: "A Remote Terminal Unit can be used for communication externally. Asset taken from the review of industry SAS designs."
	{
	}
	asset Server extends IcsApplication
	user info: "Asset taken from SCL-Lang. Logical Nodes in a server can be reached outside of the subnetwork. The server can reside for example in an IED or a MU."
	{
	}
	asset SubEquipment extends Equipment 
	user info: "Asset taken from SCL-Lang. Each phase of a conducting equipment is defined as a subequipment."
	{
	}
	asset SubFunction extends Function 
	user info: "Asset taken from SCL-Lang. Subpart of Function."
	{
	}

	asset SubNetwork extends Network 
	user info: "Taken from SCL-Lang. SubNetwork is IEC 61850 terminology."
	{
	}

	asset SubstationServer extends IcsApplication
	user info: " Asset taken from the review of industry SAS designs. Since Server means something else in the IEC 61850 terminology, SubstationServer is a typical IT server found in a substation and can be for monitoring, security or power quality control."
	{
	}
	asset Transformer extends Equipment 
	user info: "Asset taken from SCL-Lang. Alters the voltage or current level."
	{
	}
}
associations {
	ActuatorCB [actuatorCB] * <-- CloseOrTrip --> * [circuitBreaker] CircuitBreaker
	Clock [clock] 0..1 <-- Communication --> 1 [accessPoint] AccessPoint
	Function [function] * <-- Communication --> * [logicalNode] LogicalNode
	IEDRouter [iedRouter] * <-- SubnetConnection --> 1..* [accessPoint] AccessPoint
	LogicalNode [logicalNode] * <-- Represents --> * [equipment] Equipment
	LogicalNode [logicalNode] * <-- Represents --> * [actuator] Actuator 
}
